DeviceRegistryEvents
| where InitiatingProcessParentFileName =~ "userinit.exe" and ActionType == "RegistryValueSet"
| where RegistryKey endswith @"Microsoft\Windows\CurrentVersion\Explorer\RunMRU"
//looks for any pattern of nslookup, Resolve-DnsName, or it being obsfucated
| where (RegistryValueData matches regex @"(?i)\bn[^a-z0-9\s]*s[^a-z0-9\s]*l[^a-z0-9\s]*o[^a-z0-9\s]*o[^a-z0-9\s]*k[^a-z0-9\s]*u[^a-z0-9\s]*p\b"
        or RegistryValueData matches regex @"(?i)r[^a-z0-9]*e[^a-z0-9]*s[^a-z0-9]*o[^a-z0-9]*l[^a-z0-9]*v[^a-z0-9]*e[^a-z0-9]*-?[^a-z0-9]*d[^a-z0-9]*n[^a-z0-9]*s[^a-z0-9]*n[^a-z0-9]*a[^a-z0-9]*m[^a-z0-9]*e")
| project Timestamp, ActionType, InitiatingProcessParentFileName, RegistryKey, RegistryValueData
| order by Timestamp desc 
//reference: https://www.bleepingcomputer.com/news/security/new-clickfix-attack-abuses-nslookup-to-retrieve-powershell-payload-via-dns/
//regex test of nslookup: https://regex101.com/r/1blZ57/2
//regex test of Resolve-DnsName: https://regex101.com/r/hZvkre/1
